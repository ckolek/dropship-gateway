buildscript {
  apply from: "$rootDir/gradle/dependencies.gradle"

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

plugins {
  id 'idea'
  id 'nebula.nebula-release' version '4.0.1'
  id 'io.freefair.lombok' version '6.0.0-m2'
  id 'org.springframework.boot' version '2.4.5'
  id 'com.palantir.docker' version '0.26.0'
}

import org.springframework.boot.gradle.plugin.SpringBootPlugin

group 'me.kolek.ecommerce.dsgw'

allprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'io.freefair.lombok'

  version = rootProject.version

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(16)
    }
  }

  compileJava {
    options.compilerArgs << '-parameters'
  }

  dependencies {
    testImplementation "junit:junit"
  }

  test {
    useJUnitPlatform()
  }
}

configure(subprojects.findAll { !(it.name in ['api-model', 'order-event-handler']) }) {
  dependencies {
    implementation enforcedPlatform(SpringBootPlugin.BOM_COORDINATES)

    implementation libraries.guava
    implementation libraries.javaxInject
  }
}

configure(subprojects.findAll { it.name in ['api', 'worker'] }) {
  apply from: "$rootDir/gradle/springBoot.gradle"
  apply from: "$rootDir/gradle/docker.gradle"

  dependencies {
    implementation project(':core')

    implementation libraries.springBootStarterActuator
    implementation libraries.springBootStarterWeb

    testImplementation testLibraries.springBootStarterTest

    runtimeOnly libraries.postgresql
  }
}
